resultsCache = []
closed = null
keydownListening = false

onDocumentKeydown = (e) ->
  if e.key is "ArrowDown" or e.key is "ArrowUp"
    $records = $('.<%= classname_prefix %>-searches-autocomplete__record')
    focused = null
    $records.each (i, el) ->
      if el.classList.contains('<%= classname_prefix %>-searches-autocomplete__record--focused')
        focused = i
        return false

    if focused isnt null
      shift = if e.key is "ArrowDown" then 1 else -1
      target = focused + shift
      if target < -1
        target = -1
      else if target > $records.length - 1
        target = 0
    else
      target = if e.key is "ArrowDown" then 0 else -1

    $records
      .removeClass('<%= classname_prefix %>-searches-autocomplete__record--focused')
      .eq(target)
      .addClass('<%= classname_prefix %>-searches-autocomplete__record--focused')

  else if e.key is "Enter"
    focused = document.querySelector('.<%= classname_prefix %>-searches-autocomplete__record--focused')
    if focused
      e.preventDefault()
      e.stopPropagation()
      focused.click()

startAutocomplete = ($wrap) ->
  $wrap
    .addClass('<%= classname_prefix %>-ui-header-search--autocomplete')

  unless keydownListening
    keydownListening = true
    $(document).on 'keydown.vUiHeaderSearch', onDocumentKeydown

stopAutocomplete = ($wrap) ->
  $wrap
    .removeClass('<%= classname_prefix %>-ui-header-search--autocomplete')
    .find('.<%= classname_prefix %>-ui-header-search__autocomplete-results')
    .html('')

  if keydownListening
    keydownListening = false
    $(document).off 'keydown.vUiHeaderSearch'

close = ($wrap) ->
  closed = Number(new Date())

  stopAutocomplete($wrap)

  $wrap.removeClass('<%= classname_prefix %>-ui-header-search--expanded')

  $(window).trigger('resize.uiHeaderMenu')

getCachedResult = (q) ->
  result = null
  resultsCache.forEach (ary) -> result = ary[1] if q is ary[0]
  return result

loadAutocomplete = (input) ->
  $input = $(input)
  $wrap = $input.closest('.<%= classname_prefix %>-ui-header-search')
  value = input.value
  cachedResult = getCachedResult(value)

  if cachedResult
    $wrap
      .find('.<%= classname_prefix %>-ui-header-search__autocomplete-results')
      .html(cachedResult)

    return

  $.ajax
    url: input.getAttribute('data-autocomplete-url')
    data:
      q: value
    method: 'GET'
    success: (response) ->
      $wrap
        .find('.<%= classname_prefix %>-ui-header-search__autocomplete-results')
        .html(response.data)

      resultsCache = resultsCache.slice(0, 4)
      resultsCache.unshift([value, response.data])
    error: ->
      stopAutocomplete($wrap)

debouncedLoadAutocomplete = window.folioDebounce(loadAutocomplete, 300)

$(document)
  .on 'click', '.<%= classname_prefix %>-ui-header-search__a', (e) ->
    $this = $(this)
    $wrap = $this.closest('.<%= classname_prefix %>-ui-header-search')

    isDesktop = $wrap.find('.<%= classname_prefix %>-ui-header-search__mq:visible').length
    return unless isDesktop

    e.preventDefault()

    if $wrap.hasClass('<%= classname_prefix %>-ui-header-search--expanded') or (closed and (Number(new Date()) - closed < 500))
      $form = $wrap.find('form')

      if $form.find('.<%= classname_prefix %>-ui-header-search__input').val()
        $form.submit()
      else
        close($wrap)
    else
      $wrap
        .addClass('<%= classname_prefix %>-ui-header-search--expanded')
        .find('.<%= classname_prefix %>-ui-header-search__input')
        .focus()

      $(window).trigger('resize.uiHeaderMenu')

  .on 'blur', '.<%= classname_prefix %>-ui-header-search__input', ->
    if @value is ""
      close($(this).closest('.<%= classname_prefix %>-ui-header-search'))

  .on 'keyup', '.<%= classname_prefix %>-ui-header-search__input', (e) ->
    if e.key is 'ArrowUp' or e.key is 'ArrowDown'
      e.preventDefault()
      return

    if e.key is 'Enter' and $('.<%= classname_prefix %>-searches-autocomplete__record--focused').length
      e.preventDefault()
      return

    if e.key is 'Escape'
      e.preventDefault()
      @value = ""
      return $(this).blur()

    if @value is ""
      stopAutocomplete($(this).closest('.<%= classname_prefix %>-ui-header-search'))
    else
      cachedResult = getCachedResult(@value)

      $wrap = $(this).closest('.<%= classname_prefix %>-ui-header-search')
      startAutocomplete($wrap)

      $wrap
        .find('.<%= classname_prefix %>-ui-header-search__autocomplete-results')
        .html(cachedResult or '')

      debouncedLoadAutocomplete(this) unless cachedResult

  .on 'change', '.<%= classname_prefix %>-ui-header-search__input', ->
    $(this)
      .closest('.<%= classname_prefix %>-ui-header-search')
      .toggleClass('<%= classname_prefix %>-ui-header-search--expanded', if @value then true else false)

  .on 'click', '.<%= classname_prefix %>-ui-header-search__overlay', ->
    $wrap = $(this).closest('.<%= classname_prefix %>-ui-header-search')
    close($wrap)
